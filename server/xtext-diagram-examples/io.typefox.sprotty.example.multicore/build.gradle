/*
 * Copyright (C) 2017 TypeFox and others.
 *
 * Licensed under the Apache License, Version 2.0 (the "License"); you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at http://www.apache.org/licenses/LICENSE-2.0
 */

dependencies {
	compile "org.eclipse.xtext:org.eclipse.xtext:$versions.xtext_example"
	compile "org.eclipse.xtext:org.eclipse.xtext.xbase:$versions.xtext_example"
}

jar {
	from('model') {
		into('model')
	}
}

configurations {
	mwe2 {
		extendsFrom compile
	}
}

dependencies {
	mwe2 "org.eclipse.xtext:org.eclipse.xtext.common.types:$versions.xtext_example"
	mwe2 "org.eclipse.xtext:org.eclipse.xtext.xtext.generator:$versions.xtext_example"
	mwe2 "org.eclipse.emf:org.eclipse.emf.mwe2.launch:2.9.0.201605261059"
	mwe2 "org.eclipse.xtext:xtext-antlr-generator:2.1.1"
}

task generateXtextLanguage(type: JavaExec) {
	main = 'org.eclipse.emf.mwe2.launch.runtime.Mwe2Launcher'
	classpath = configurations.mwe2
	inputs.file "src/main/java/io/typefox/sprotty/example/multicore/GenerateMulticoreAllocation.mwe2"
	inputs.file "src/main/java/io/typefox/sprotty/example/multicore/MulticoreAllocation.xtext"
	outputs.dir "src/main/xtext-gen"
	args += "src/main/java/io/typefox/sprotty/example/multicore/GenerateMulticoreAllocation.mwe2"
	args += "-p"
	args += "rootPath=/${projectDir}/.."
}

generateXtext.dependsOn(generateXtextLanguage)
clean.dependsOn(cleanGenerateXtextLanguage)
eclipse.classpath.plusConfigurations += [configurations.mwe2]
